plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version "8.1.1"
    id 'kr.entree.spigradle' version '2.4.2'
}

group = 'li.sopht'
version = '2.0-SNAPSHOT'

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = "yes"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_11

    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    implementation 'com.zaxxer:HikariCP:5.0.1'

    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'me.clip:placeholderapi:2.11.2'
    compileOnly spigot('1.19.2')
}

shadowJar {
    minimize()
    classifier ''

    relocate 'com.zaxxer.hikari', 'li.sopht.limitedplaytime.lib.hikari'
    relocate 'org.slf4j', 'li.sopht.limitedplaytime.lib.slf4j'
}

tasks.assemble.dependsOn(shadowJar)

spigot {
    debug {
        buildVersion = '1.19.2'
    }

    authors 'Sophtli'
    website 'sopht.li'
    apiVersion '1.13'
    softDepends 'PlaceholderAPI'
    commands {
        playtime {
            aliases = ['pt', 'ptime']
            description = 'Display your playtime!'
            usage = '/playtime'
        }
    }
    /*permissions {
        'test.foo' {
            description = 'Allows foo command'
            defaults = 'op'
        }
    }*/
}